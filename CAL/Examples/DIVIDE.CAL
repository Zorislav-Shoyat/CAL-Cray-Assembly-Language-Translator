         OPTIONS LIST
*         OPTIONS SHOWSF
*         OPTIONS SHOWSTAT
****************************************
*23456789012345678901234567890123456789
*        111111111122222222222333333333
*xxx     yyy       zzz            comm
****************************************
*
* ZORISLAV SHOYAT, 31/3/2014, 3:55
*
* FIRST VIRTUE KERNELS FOR CRAY-1
* .. DIVISION
*
* A1 RIGHT (TOP OF STACK)
* A2 LEFT
* A6 VECTOR LENGTH
* A7 RESULT
*
****************************************
         START     COMPUTE
COMPUTE  =         *
         S2        RT
DIVIDE   =         *

         J         RECIPR

* REAL / REAL
         A1        RIGHT
         A2        LEFT
         A7        RESULT
         A6        256
         R         $DIVRR
*
* 1 / REAL
RECIPR   A1        RIGHT
         A7        RESULT
         A6        256
         R         $RCPRR
*
         J         EOPROG
*
*

******************************************
*
* ZORISLAV SHOYAT, 31/3/2014, 3:55
* ATELIER, SHANTI
*
******************************************
*
* DIVISIONS
*
******************************************

*******************************************
** REAL NUMBERS                          **
*******************************************

*******************************************
*
* DIVIDE LEFT REAL WITH RIGHT REAL
*
* NAME: $DIVRR
*
* DESCRIPTION:
*        DIVIDE REAL WITH REAL
*
* ENTRY: A1 = RIGHT REAL DIVISOR ARGUMENT ADDRESS
*        A2 = LEST REAL DIVIDEND ARGUMENT ADDRESS
*        A7 = REAL RESULT SPACE ADDRESS
*        A6 = VECTOR LENGTH
*
* EXIT: RESULT IN MEMORY AT A7
*
* USES: 
*
* RESTRICTION:
*
* BEWARE: OPTIMISED (ADDRESS CALCULATIONS)
*
******************************************
*
* A5 = 1 FOR REAL NUMBERS, ERGO NOT USED
*
$DIVRR   =         *
* SET A5 TO 1 FOR VECTOR STRIDE ON REAL NUMBERS
*         A5        1
* PROCESS VECTORS LONGER THAN 64 ELEMENTS
         A4        A6            * PRESERVE LENGTH
         S2        <6
         S1        A6
         S1        S1&S2
         A6        S1
         S0        S1
         JSN       $DIVRR$L
         A6        64
*
* PROCESS:
*    V2 = LEFT REAL (STRIDE 1)
*    V1 = RIGHT REAL (STRIDE 1)
*    V7 = RESULT REAL
*
* LOAD AND MULTIPLY
$DIVRR$L VL        A6
         A0        A1
         V1        ,A0,1            * B
         A0        A2
         V2        ,A0,1            * A
         V3        /HV1             * N
         V4        V2*FV3           * A' = A *F N
         V5        V1*IV3           * B' = B *I N
         A2        A1+A6            * STRIDE 1 = REAL
         A1        A2+A6            * STRIDE 1 = REAL
         A4        A4-A6
         A0        A7
         V7        V4*FV5           * R = A' *F B'
         ,A0,1     V7
         A7        A7+A6
*
* CHECK FOR MORE ELEMENTS
         A0        A4
         A6        64         
         JAN       $DIVRR$L
*
         J         B00

*****************************************************

*******************************************
*
* RECIPROCAL OF RIGHT REAL
*
* NAME: $RCPRR
*
* DESCRIPTION:
*        RECIPROCAL OF REAL
*
* ENTRY: A1 = RIGHT REAL ARGUMENT ADDRESS
*        A7 = REAL RESULT SPACE ADDRESS
*        A6 = VECTOR LENGTH
*
* EXIT: RESULT IN MEMORY AT A7
*
* USES: 
*
* RESTRICTION:
*
* BEWARE: OPTIMISED (ADDRESS CALCULATIONS)
*
******************************************
*
* A5 = 1 FOR REAL NUMBERS, ERGO NOT USED
*
$RCPRR   =         *
* SET A5 TO 1 FOR VECTOR STRIDE ON REAL NUMBERS
*         A5        1
* PROCESS VECTORS LONGER THAN 64 ELEMENTS
         A4        A6            * PRESERVE LENGTH
         S2        <6
         S1        A6
         S1        S1&S2
         A6        S1
         S0        S1
         JSN       $DIVRR$L
         A6        64
*
* PROCESS:
*    V2 = LEFT REAL (STRIDE 1)
*    V1 = RIGHT REAL (STRIDE 1)
*    V7 = RESULT REAL
*
* LOAD AND MULTIPLY
$RCPRR$L VL        A6
         A0        A1
         V1        ,A0,1            * B
         V2        ,A0,1            * V2 V1
         V3        /HV1             * N
         V5        V2*IV3
         A2        A1+A6            * STRIDE 1 = REAL
         A1        A2+A6            * STRIDE 1 = REAL
         A4        A4-A6
         A0        A7
         V7        V5*FV3           * R = A' *F B'
         ,A0,1     V7
         A7        A7+A6
*
* CHECK FOR MORE ELEMENTS
         A0        A4
         A6        64         
         JAN       $RCPRR$L
*
         J         B00

*****************************************************


*******************************************
*******************************************
*******************************************
EOPROG   =        *
         S1       RT
         S2       S1-S2
         ENDP


LEFT     DATA      *,256
ENDLEFT  =         *
         DATA      *,1                 * THE DATA SHALL NOT BE ALIGNED BETWEEN
*                                      * BLOCKS TO PREVENT SAME LINE CONFLICT
RIGHT    DATA      *,2048
         DATA      *,1
RESULT   DATA      *,2048

******************************************************************
*
******************************************************************

DATASPAC =         *
* Virtue OUTPUT GOES HERE: (SEE PROGRAMME BELOW)
*
*
ENDDATA  =         *

         EXEC      *,*

         END
